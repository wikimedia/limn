limn = require 'limn'
DataSource = limn.data.DataSource

{ TimeSeriesData, CSVData,
} = require 'timeseries'


# XXX: Hear ye.
# 
# If we don't purge the cache between tests, accidentally re-using DataSource IDs in
# different tests will cause random things to hang or fail -- the object is already
# in-cache, which means (for example) negative existence tests fail and `subscribe`
# callbacks often won't fire due to a cache hit on first retrieval (so it never
# changes).
# 
# Fun times. That only took an hour to figure out.
QUnit.module '[data] DataSource', do
    setup    : -> DataSource.__cache__.purge()
    teardown : -> DataSource.__cache__.purge()

/******** DataSource cache ********/
asyncTest 'create two DataSources', ->
    expect 5
    start()
    
    sameId = 'rc_edits_count'
    fetching1 = DataSource.lookup sameId
    fetching2 = DataSource.lookup sameId
    
    ds1 = DataSource.get sameId
    ds2 = DataSource.get sameId
    
    strictEqual ds1, ds2, 'only one DataSource will get created per id if using lookup'
    strictEqual fetching1, fetching2, 'only one promise to fetch will get created by lookup'
    
    stop()
    fetching2.done ->
        start()
        ok true, 'second datasource is fetched'
        
        stop()
        fetching1.done ->
            start()
            ok ds1.name() != undefined, 'model attributes get loaded'
            ok ds1.name() != "", 'model attributes get loaded'


asyncTest 'lazy-load DataSource data', ->
    expect 8
    
    id = 'rc_page_requests'
    DataSource.lookup id, (err, ds) ->
        start()
        
        equal err, null, "No errors on DataSource.lookup()."
        ok not ds.isNew(), "DataSource has been loaded from server."
        ok ds.get('url'), "DataSource has been loaded and has a valid data URL."
        strictEqual ds.data.inProgress(), false, "DataSource.data.inProgress is false at first."
        equal ds.data(), null, "DataSource.data() is undefined at first (this check triggers loading)"
        strictEqual ds.data.inProgress(), true, "DataSource.data.inProgress is true now that we're loading."
        
        stop()
        ds.data.subscribe ->
            start()
            
            ok ds.data(), "DataSource.data() now defined"
            ok ds.data() instanceof CSVData, "DataSource.data() instanceof CSVData"


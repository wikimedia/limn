_  = require 'underscore'
d3 = require 'd3'
ko = require 'knockout'
op = require 'operator'

{ ViewportNodeData, ViewportNode,
} = require './viewport-node'

Trait = require './graph-node-trait'


toNumeric = -> +it

cmpNumeric = (a, b) ->
    op.cmp +a, +b

tuplesEqual = (a, b) ->
    cmpNumeric(a[0], b[0]) is 0 and
    cmpNumeric(a[1], b[1]) is 0





/**
 * @class
 * @extends ViewportNodeData
 */
class exports.CanvasNodeData extends ViewportNodeData
    @registerType 'canvas'
    
    defaults: -> {}
    attributeTypes: -> {}
    
    -> super ...



/**
 * @class
 * @extends ViewportNode
 */
class exports.CanvasNode extends ViewportNode
    @registerType 'canvas'
    traits   : [ Trait.VIEWPORT ]
    tagName  : 'section'
    template : 'canvas-node'
    
    ->
        super ...
        @on 'child-built', @resize, this # Trigger resize whenever a child is added
        $ window .resize _.debounce @resize.bind(this), 50ms
    
    
    
    buildElement: (parentElement) ->
        @renderView parentElement
        @el el = $(parentElement).find('.canvas-node.graph-node').0
        @trackCursor()
        el
    
    
    /**
     * Element representing the frame where the content is held.
     * @type ko.observable<d3.selection>
     */
    contentFrameEl: @computed ->
        d3.select @$('.content-frame').0 if @el()
    
    
    /**
     * Determine the DOM node to act as the parent for a direct child of the root node.
     * 
     * @protected
     * @param {GraphNode} node
     * @returns {Element}
     */
    determineDOMParentForChildNode: (node) ->
        # SVG nodes
        if node.hasTrait Trait.SVG
            if node.hasTrait Trait.FG_LAYER
                @$ 'svg .frame > .fg' .0
            else if node.hasTrait Trait.BG_LAYER
                @$ 'svg .content-frame > .bg' .0
            else
                @$ 'svg .content-frame > .viewport' .0
        
        # HTML nodes
        else
            # XXX: Hack
            if node.hasTrait Trait.CALLOUT_NODE
                @graph.$ '.graph-name-row > .callout' .0
            else if node.hasTrait Trait.SECTION
                @$el.parent().0
            else if node.hasTrait Trait.FG_LAYER
                @$ '.meta.fg' .0
            else
                @$ '.meta.bg' .0
    
    
    background: (color) ->
        return unless (bg = @$ '.content-frame > .bg > .bgcolor').length
        if arguments.length
            bg.css 'fill', color
            this
        else
            bg.css 'fill'
    
    
    /**
     * Current location of the cursor in the viewport.
     * @type ko.observable<Object>
     */
    cursor: ko.observable null
    
    
    trackCursor: ->
        return if @cursor.peek() # only initialize once
        return unless el  = _.toElement @contentFrameEl()
        return unless svg = _.toElement @graph.$('svg')
        svg = d3.select svg
        
        svg.on 'mouseover', ~>
            [x, y] = d3.mouse(el)
            cursor = { +hovering, x, y }
            @trigger 'mouseover', cursor, this
            # cursor observable will be handled by mousemove
        
        svg.on 'mousemove', ~>
            [x, y] = d3.mouse(el)
            cursor = { +hovering, x, y }
            @trigger 'mousemove', cursor, this
            with @cursor.peek()
                @cursor cursor if not &?.hovering or &x != x or &y != y
        
        svg.on 'mouseout', ~>
            [x, y] = d3.mouse(el)
            @cursor cursor = { -hovering, x, y }
            @trigger 'mouseout', cursor, this
        
        this
    
    


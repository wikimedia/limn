_ = require 'underscore'
ko = require 'knockout'

{ View,
} = require '../base/view'
{ Graph,
} = require './graph-model'
{ GraphView,
} = require './graph-view'
{ DataSource, ColumnDef,
} = require '../data/datasource'



class exports.GraphCreateView extends View
    template: 'graph-create'
    
    
    /**
     * @constructor
     */
    ->
        super()
        
        @blankGraph = ko.observable()
        @metrics = ko.observableArray()
        @previewDialog = ko.observable()
        @recentGraphs = ko.observableArray()
        
        @datasources = ko.observableArray()
        @filter = ko.observable()
        @datasourceType = ko.observable 'timeseries'
        @filteredDatasources = ko.computed ~>
            filter = (@filter() or '').toLowerCase()
            typeFilter = @datasourceType().toLowerCase()
            @datasources().filter ~>
                id = it.id().toLowerCase()
                name = it.name().toLowerCase()
                type = it.type().toLowerCase()
                return type is typeFilter
                   and (not filter.length
                         or id.indexOf(filter) >= 0
                         or name.indexOf(filter) >= 0)
    
    
    newGraph: ->
        console.log 'submitted'
    
    
    afterRender: (element) ->
        return if @el()
        @el element
        
        @initializeAddForm()
        DataSource.fetchAll().done ~>
            @datasources(_.sortBy DataSource.getAllSources(), (d) -> d.name())
            @$('.datasourceSearch').typeahead({source: @datasources().map -> it.name()})
    
    
    initializeAddForm: ->
        blankGraph = new Graph do
            root:
                nodeType: 'canvas'
                y:
                    scaleType: 'linear'
                width: 900
                height: 350
                children: [
                    {nodeType: 'axis', options: {dimension: 'x', orient: 'bottom'}}
                    {nodeType: 'axis', options: {dimension: 'y', orient: 'left'}}
                    {nodeType: 'grid', options: {dimension: 'x'}}
                    {nodeType: 'grid', options: {dimension: 'y'}}
                    {nodeType: 'legend', options: {dateFormat: 'HH:mm on MMM DD, YYYY'}}
                    {nodeType: 'zoom-brush'}
                    {
                        nodeType: "line-group",
                        options: {palette: 'category10'},
                        children: []
                    }
                ]
        
        if @blankGraph()
            blankGraph.name @blankGraph().name()
            blankGraph.slug @blankGraph().slug()
            blankGraph.desc @blankGraph().desc()
            blankGraph.notes @blankGraph().notes()
        
        @blankGraph blankGraph
    
    
    refreshMetrics: ->
        lineNodes = @metrics().map ->
            {
                nodeType: 'line'
                metric: {
                    source_id: it.source.id()
                    source_col: it.index()
                    type: it.type()
                }
            }
        
        lineGroup = _.find @blankGraph().root().children(), -> it.nodeType is 'line-group'
        lineGroup.children lineNodes
    
    
    addDatasource: (datasource) ->
        ~>
            metrics = @metrics.peek()
            for column of datasource.columns()
                if column.type() is not 'date'
                    metrics.push(column)
            @metrics _.sortBy metrics, (c) -> c.source.name()
            @refreshMetrics()
    
    
    removeMetric: (metric) ->
        ~>
            @metrics.remove metric
            @refreshMetrics()
    
    
    keepOnlyThisMetric: (metric) ->
        ~>
            metrics = _.filter @metrics.peek(), (m) ->
                m.source.id() != metric.source.id() || m.id() == metric.id()
            @metrics metrics
            @refreshMetrics()
    
    
    preview: ->
        limn.view.previewDialog new GraphView this
        limn.view.$('> .previewDialog').modal()
        limn.view.previewDialog().resize()
    
    
    cleanUpPreview: ->
        @initializeAddForm()
        @refreshMetrics()
    
    
    newGraph: ->
        @blankGraph().save()
            .done ~>
                @blankGraph().id @blankGraph().slug()
                limn.info "Saved Successfully with id #{@blankGraph().id()}"
                @recentGraphs.push @blankGraph()
                @cleanUpPreview()
            .fail (data, message, response) ->
                switch response.status
                    case 403
                        limn.error "Not Saved.  Please sign in (top right of this page)"
                    case 409
                        limn.error "Not Saved.  Graph '#{@slug()}' already exists"
                    default
                        limn.error 'Not Saved.  There was a problem'
    
    
    deleteGraph: (graph, event) ->
        graph.destroy()
            .done ->
                limn.info "Deleted Graph #{graph.id()}"
                ko.contextFor(event.target).$parents[1].recentGraphs.remove graph
            .fail ->
                limn.error 'Not Deleted.  There was a problem'

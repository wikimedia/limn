_  = require 'kraken/util/underscore'
TimeSeriesData = require 'kraken/util/timeseries/timeseries'


DASH_PATTERN       = /-/g
BLANK_LINE_PATTERN = /^(\s*)$/
COMMENT_PATTERN    = /\s*(#|\/\/).*$/

class CSVData extends TimeSeriesData
    DEFAULT_OPTIONS :
        colSep              : ','
        rowSep              : '\n'
        defaultType         : 'float'
        customBars          : false
        customSep           : ';'
        errorBars           : false
        fractions           : false
        fractionSep         : '/'
        skipBlankLines      : true
        blankLinePat        : BLANK_LINE_PATTERN
        removeCommentedText : true
        commentPat          : COMMENT_PATTERN
        replaceMissing      : false
        replaceMissingValue : 0
        replaceNaN          : false
        replaceNaNValue     : 0
        padRows             : false
        padRowsValue        : 0
    
    
    (data, opts) ->
        super ...
    
    
    /* * * *  CSV Parsing  * * * */
    
    parseNumber: (s) ->
        parseFloat s
    
    parseHiLo: (s) ->
        s.split @options.customBars .map @parseNumber, this
    
    parseFraction: (s) ->
        s.split @options.fractionSep .map @parseNumber, this
    
    parseDate: (s) ->
        new Date s.replace DASH_PATTERN, '/'
    
    
    /**
     * Parses and imports a CSV string.
     * 
     * @private
     * @returns {this}
     */
    parseData: (@rawData) ->
        return this if typeof rawData is not 'string'
        o = @options
        
        lines = rawData.split o.rowSep
        return [] unless lines.length
        first = lines[0]
        
        # Use the default delimiter or fall back to a tab if that makes sense.
        delim = o.colSep
        if first.indexOf(delim) is -1 and first.indexOf('\t') >= 0
            delim = '\t'
        
        rows = @rows = []
        @columns     = []
        
        parser = @parseNumber
        parser = @parseHiLo     if o.customBars
        parser = @parseFraction if o.fractions
        
        hasHeaders = @labels.length is not 0
        for line, i of lines
            line .= replace o.commentPat, '' if o.removeCommentedText
            continue if o.skipBlankLines and (line.length is 0 or o.blankLinePat.test line)
            
            cols = line.split delim
            unless hasHeaders
                hasHeaders = true
                @labels = cols.map -> _.strip it
                continue
            
            continue unless cols.length > 1
            date = @parseDate cols.shift()
            fields = cols.map parser, this
            if o.errorBars
                fields = fields.reduce do
                    (acc, v) ->
                        last = acc[acc.length-1]
                        unless last and last.length < 2
                            acc.push last = []
                        last.push v
                        acc
                    []
            
            fields.unshift date
            rows.push fields
            fields.forEach (v, idx) ~>
                @columns.push [] unless @columns[idx]
                @columns[idx].push v
        
        @untransformedRows = _.merge [], @rows
        this
    
    
    
    

module.exports = exports = CSVData



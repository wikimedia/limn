Base = require '../../base/base'


/**
 * @class An EventEmitter that auto-triggers new handlers once "ready".
 */
class ReadyEmitter extends Base
    readyEventName : 'ready'
    ready : false
    
    /**
     * Triggers the 'ready' event if it has not yet been triggered.
     * Subsequent listeners added to this event will be auto-triggered.
     * @param {Boolean} [force=false] Trigger the event even if already ready.
     * @returns {this}
     */
    triggerReady: (force) ->
        return this if @ready and not force
        @ready = true
        @emit @readyEventName, this
        this
    
    /**
     * Resets the 'ready' event to its non-triggered state, firing a
     * 'ready-reset' event.
     * @param {Boolean} [force=false] Trigger the event even if already reset.
     * @returns {this}
     */
    resetReady: (force) ->
        return this unless @ready and not force
        @ready = false
        @emit "#{@readyEventName}-reset", this
        this
    
    
    /**
     * Wrap {@link EventEmitter#on} registration to handle registrations
     * on 'ready' after we've broadcast the event. Handler will always still
     * be registered, however, in case the emitter is reset.
     * 
     * @param {String} events Space-separated events for which to register.
     * @param {Function} callback
     * @param {Object} [context]
     * @returns {this}
     */
    on: (events, callback, context=this) ->
        return this if not callback
        super ...
        if @ready and -1 is not events.split(/\s+/).indexOf @readyEventName
            setTimeout ~> callback.call context, this
        this
    
    /**
     * Copy ReadyEmitter instance methods (and EventEmitter) methods to target.
     * @returns {Object} The target object.
     */
    @decorate = (target) ->
        for k of <[ readyEventName ready triggerReady resetReady on     emit trigger on off addListener removeListener removeAllListeners once listeners ]>
            target[k] = ReadyEmitter.prototype[k]
        target
    


module.exports = exports = ReadyEmitter

Base = require '../../base/base'


/**
 * @class An EventEmitter with a ratchet-up waiting counter.
 * @extends Base
 */
class WaitingEmitter extends Base
    
    /**
     * Count of outstanding tasks.
     * @type Number
     */
    waitingOn : 0
    
    
    /**
     * Increment the waiting task counter.
     * @returns {this}
     */
    wait: ->
        count = @waitingOn
        @waitingOn += 1
        # console.log "#this.wait! #count --> #{@waitingOn}"
        # console.trace()
        @trigger('start-waiting', this) if count is 0 and @waitingOn > 0
        this
    
    /**
     * Decrement the waiting task counter.
     * @returns {this}
     */
    unwait: ->
        count = @waitingOn
        @waitingOn -= 1
        # console.warn "#this.unwait! #{@waitingOn} < 0" if @waitingOn < 0
        # console.log "#this.unwait! #count --> #{@waitingOn}"
        # console.trace()
        @trigger('stop-waiting', this) if @waitingOn is 0 and count > 0
        this
    
    /**
     * @param {Function} fn Function to wrap.
     * @returns {Function} A function wrapping the passed function with a call
     *  to `unwait()`, then delegating with current context and arguments.
     */
    unwaitAnd: (fn) ->
        self = this
        ->
            # console.log "#self.unwaitAnd( function #{fn.name or fn.displayName}() )"
            # console.trace()
            self.unwait(); fn ...
    
    /**
     * Copy WaitingEmitter instance methods (and EventEmitter) methods to target.
     * @returns {Object} The target object.
     */
    @decorate = (target) ->
        for k of <[ waitingOn wait unwait unwaitAnd     emit trigger on off addListener removeListener removeAllListeners once listeners ]>
            target[k] = WaitingEmitter.prototype[k]
        target
    

module.exports = exports = WaitingEmitter

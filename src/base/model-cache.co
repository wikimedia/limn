_   = require 'underscore'
Seq = require 'seq'

{ EventEmitter, ReadyEmitter
} = require 'emitters'

Deferred = jQuery.Deferred


# TODO: Bubble events to decorated emitters
# TODO: Automatically create a cache for any class that extends BaseModel
/**
 * @class Caches models and provides static lookups by ID.
 */
class exports.ModelCache extends ReadyEmitter
    
    /**
     * @see ReadyEmitter#readyEventName
     * @private
     * @constant
     * @type String
     */
    __ready_event__ : 'cache-ready'
    
    /**
     * Default options.
     * @private
     * @constant
     * @type Object
     */
    DEFAULT_OPTIONS:
        ready     : true
        cache     : null
        create    : null
        ModelType : null
    
    /**
     * @private
     * @type Object
     */
    options : null
    
    /**
     * Type we're caching (presumably extending `Model`), used to create new
     * instances unless a `create` function was provided in options.
     * @private
     * @type Class<Model>
     */
    ModelType : null
    
    /**
     * Map holding the cached Models.
     * @private
     * @type Map<id, Model>
     */
    cache : null
    
    /**
     * Holds promises for fetch requests: Map from id to Deferred
     * @private
     * @type Object
     */
    promises : null
    
    
    
    /**
     * @constructor
     * @param {Class<Model>} [ModelType] Type of cached object (presumably extending
     *  `Model`), used to create new instances unless `options.create`
     *  is provided.
     * @param {Object} [options] Options:
     * @param {Boolean} [options.ready=true] Starting `ready` state. If false,
     *  the cache will queue lookup calls until `triggerReady()` is called.
     * @param {Object} [options.cache={}] The backing data-structure for the cache.
     * @param {Function} [options.create] A function called when a new Model
     *  object is needed, being passed the new model ID.
     * @param {Class<Model>} [options.ModelType] Type of cached object
     *  (presumably extending `Model`), used to create new instances
     *  unless `options.create` is provided.
     */
    (ModelType, options) ->
        unless _.isFunction ModelType
            [options, ModelType] = [ModelType or {}, null]
        @options = {...@DEFAULT_OPTIONS, ...options}
        
        @promises = {}
        @cache = @options.cache or {}
        
        @ModelType = ModelType or @options.ModelType
        @createModel = that if @options.create
        
        @_ready = !!@options.ready
        @decorate @ModelType if @ModelType
    
    
    /**
     * Called when a new Model object is needed, being passed the new model ID.
     * Uses the supplied `ModelType`; overriden by `options.create` if provided.
     * 
     * @param {String} id The model ID to create.
     * @returns {Model} Created model.
     */
    createModel: (id) ->
        new @ModelType {id}
    
    /**
     * Synchronously check if a model is in the cache, returning it if so.
     * 
     * @param {String} id The model ID to get.
     * @returns {Model}
     */
    get: (id) ->
        @cache[id]
    
    /**
     * Looks up a model, requesting it from the server if it is not already
     * known to the cache.
     *
     * @param {String|Array<String>} id Model ID to lookup.
     * @param {Function} [cb] Callback of the form `(err, model)`,
     *  where `err` will be null on success and `model` will be the
     *  model object.
     * @param {Object} [cxt=this] Callback context.
     * @returns {this}
     */
    lookup: (id, cb, cxt) ->
        throw new Error "A model ID must be specified!" unless id
        model = @get id
        
        # Cache hit? Return its extant promise.
        if model
            
            # The cache can be populated via `register()` calls not originating in `lookup()`,
            # resulting in cache entries without promises. If so, we need to both create
            # the promise and resolve it before handing things back.
            deferred = @promises[id] or= new Deferred().resolve(model)
            
        # Cache miss? Create a new promise and a new request.
        else
            deferred = @promises[id] = new Deferred()
            model    = @register @createModel id
            xhr      = model.fetch()
            
            deferred.xhr = xhr.xhr
            xhr.then do
                -> deferred.resolveWith this, arguments
                -> deferred.rejectWith  this, arguments
        
        # optional callback: `cb(err, model)`
        if typeof cb is 'function'
            _.defer ~> deferred.then do
                (model)       ~> cb.call(cxt or this, null, model)
                (model, err)  ~> cb.call(cxt or this, err,  model)
        
        # Return a promise for the loaded model
        deferred
    
    /**
     * Registers a model with the cache. If a model by this ID already exists
     * in the cache, it will be removed and this one will take its place.
     *
     * Fires an `add` event.
     * 
     * @param {Model} model The model.
     * @returns {Model} The model.
     */
    register: (model) ->
        throw new Error "You must supply a model! (model=#model)" unless model
        
        id = model.getId()
        throw new Error "Supplied model has no id! (model=#model, id=#id)" unless id
        
        if @cache[id] and @cache[id] is not model
            className = @ModelType.displayName or @ModelType.name
            throw new Error "Model of type #className already exists at #id."
        @cache[id] = model
        @trigger 'add', this, model
        model
    
    
    /**
     * Decorate an object with the cache methods:
     *  - register
     *  - get
     *  - lookup
     *  - lookupAll
     * 
     * This is automatically called on `ModelType` if supplied.
     * 
     * @param {Object} obj Object to decorate.
     * @returns {obj} The supplied object.
     */
    decorate: (obj) ->
        obj.__cache__ = this
        # Bind the ModelCache methods to the class
        for m of <[ get lookup ]>
            obj[m] = @[m].bind this
        obj
    
    toString: ->
        "#{@..displayName or @..name}(cache=#{@cache})"


